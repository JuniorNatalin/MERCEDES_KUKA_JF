&ACCESS RVO4
&REL 139
&COMMENT Pega TRV LCA case
&PARAM DISKPATH = KRC:\R1\Program\LCA
DEF ho_com_01_st03_lca( )
;FOLD INI
  ;FOLD BASISTECH INI
    GLOBAL INTERRUPT DECL 3 WHEN $STOPMESS==TRUE DO IR_STOPM ( )
    INTERRUPT ON 3
    BAS (#INITMOV,0)
  ;ENDFOLD (BASISTECH INI)
;FOLD GRIPPERTECH INI
USER_GRP(0,DUMMY,DUMMY,GDEFAULT)
;ENDFOLD (GRIPPERTECH INI)
  ;FOLD USER INI
    ;Make your modifications here

  ;ENDFOLD (USER INI)
;ENDFOLD (INI)

; Caracterizacao dos meios de producao:
; Changes: Pega Travessa no LCA
; Versao Atual:10/09/2015
; Name: P13.050-R2

chk_w6_wechs_ok ( )     ; CHECK GRIPPER AXOR/ATEGO

;FOLD PTP wkz_7 Vel=100 % PDAT11 Tool[6]:Gripper G0100R Base[0];%{PE}%R 8.2.20,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:wkz_7, 3:, 5:100, 7:PDAT11
$BWDSTART=FALSE
PDAT_ACT=PPDAT11
FDAT_ACT=Fwkz_7
BAS(#PTP_PARAMS,100)
PTP Xwkz_7 
;ENDFOLD

; ABRE GRAMPOS
;FOLD SET Z2.1 RPS AXOR State=OPN CONT at START Delay=1 ms;%{PE}%R 8.2.1,%MKUKATPGRP,%CGRP,%VGRP,%P 2:10, 4:1, 5:#YES, 6:GDAT26, 8:0, 10:1
TRIGGER WHEN DISTANCE=0 DELAY=1 DO H50(GRP_APO,10,1,GCONT) PRIO=-1
;ENDFOLD
;FOLD SET Z3.1 GRP AXOR State=OPN CONT at START Delay=1 ms;%{PE}%R 8.2.1,%MKUKATPGRP,%CGRP,%VGRP,%P 2:11, 4:1, 5:#YES, 6:GDAT27, 8:0, 10:1
TRIGGER WHEN DISTANCE=0 DELAY=1 DO H50(GRP_APO,11,1,GCONT) PRIO=-1
;ENDFOLD
;FOLD SET Z4.1 GRP AXOR State=OPN CONT at START Delay=1 ms;%{PE}%R 8.2.1,%MKUKATPGRP,%CGRP,%VGRP,%P 2:12, 4:1, 5:#YES, 6:GDAT9, 8:0, 10:1
TRIGGER WHEN DISTANCE=0 DELAY=1 DO H50(GRP_APO,12,1,GCONT) PRIO=-1
;ENDFOLD
;FOLD SET Z5.1/2/3/4 GRP AXOR State=OPN CONT at START Delay=1 ms;%{PE}%R 8.2.1,%MKUKATPGRP,%CGRP,%VGRP,%P 2:13, 4:1, 5:#YES, 6:GDAT28, 8:0, 10:1
TRIGGER WHEN DISTANCE=0 DELAY=1 DO H50(GRP_APO,13,1,GCONT) PRIO=-1
;ENDFOLD
;FOLD SET Z6.1/2/3/4 GRP AXOR State=OPN CONT at START Delay=1 ms;%{PE}%R 8.2.1,%MKUKATPGRP,%CGRP,%VGRP,%P 2:14, 4:1, 5:#YES, 6:GDAT29, 8:0, 10:1
TRIGGER WHEN DISTANCE=0 DELAY=1 DO H50(GRP_APO,14,1,GCONT) PRIO=-1
;ENDFOLD

;FOLD PTP l016 CONT Vel=100 % P_OLP11 Tool[6]:Gripper G0100R Base[0];%{PE}%R 8.2.20,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:l016, 3:C_DIS, 5:100, 7:P_OLP11
$BWDSTART=FALSE
PDAT_ACT=PP_OLP11
FDAT_ACT=Fl016
BAS(#PTP_PARAMS,100)
PTP Xl016 C_DIS
;ENDFOLD
;FOLD PTP P10 CONT Vel=100 % PDAT16 Tool[6]:Gripper G0100R Base[0];%{PE}%R 8.2.20,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:P10, 3:C_DIS, 5:100, 7:PDAT16
$BWDSTART=FALSE
PDAT_ACT=PPDAT16
FDAT_ACT=FP10
BAS(#PTP_PARAMS,100)
PTP XP10 C_DIS
;ENDFOLD

; VERIFICA GRAMPOS ABERTOS
chk_w6_rps2_opn( )
chk_w6_grp3_opn( )
chk_w6_grp4_opn( )
chk_w6_grp5_opn( )
chk_w6_grp6_opn( )

;FOLD PTP l021 CONT Vel=100 % P_OLP12 Tool[6]:Gripper G0100R Base[0];%{PE}%R 8.2.20,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:l021, 3:C_DIS, 5:100, 7:P_OLP12
$BWDSTART=FALSE
PDAT_ACT=PP_OLP12
FDAT_ACT=Fl021
BAS(#PTP_PARAMS,100)
PTP Xl021 C_DIS
;ENDFOLD
;FOLD PTP l027 CONT Vel=100 % P_OLP21 Tool[6]:Gripper G0100R Base[0];%{PE}%R 8.2.20,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:l027, 3:C_DIS, 5:100, 7:P_OLP21
$BWDSTART=FALSE
PDAT_ACT=PP_OLP21
FDAT_ACT=Fl027
BAS(#PTP_PARAMS,100)
PTP Xl027 C_DIS
;ENDFOLD

;FOLD OUT 3370 'Lib. Pegar no LCA Axor/Atego' State=TRUE ;%{PE}%R 8.2.20,%MKUKATPBASIS,%COUT,%VOUTX,%P 2:3370, 3:Lib. Pegar no LCA Axor/Atego, 5:TRUE, 6:
$OUT[3370]=TRUE
;ENDFOLD
;FOLD WAIT FOR ( IN 3370 'Lib. p/ Pegar no LCA Axor/Atego' );%{PE}%R 8.2.20,%MKUKATPBASIS,%CEXT_WAIT_FOR,%VEXT_WAIT_FOR,%P 2:, 4:, 5:$IN, 6:3370, 7:Lib. p/ Pegar no LCA Axor/Atego, 9:
WAIT FOR  ( $IN[3370] ) 
;ENDFOLD
;FOLD OUT 3370 'Lib. Pegar no LCA Axor/Atego' State=FALSE ;%{PE}%R 8.2.20,%MKUKATPBASIS,%COUT,%VOUTX,%P 2:3370, 3:Lib. Pegar no LCA Axor/Atego, 5:FALSE, 6:
$OUT[3370]=FALSE
;ENDFOLD

; AGUARDA PECA NO BUFFER (LCA)
; AGUARDA POSIÇÃO VERDADEIRA
;FOLD WAIT FOR ( NOT IN 489 'Presença de peça 1 - Axor LCA' );%{PE}%R 8.2.20,%MKUKATPBASIS,%CEXT_WAIT_FOR,%VEXT_WAIT_FOR,%P 2:, 4:NOT, 5:$IN, 6:489, 7:Presença de peça 1 - Axor LCA, 9:
WAIT FOR  (  NOT $IN[489] ) 
;ENDFOLD
;FOLD WAIT FOR ( IN 3360 'LCA p.1' ) OR ( IN 3361 'LCA p.2' ) OR ( IN 3362 'LCA p.3' ) OR ( IN 3363 'LCA p.4' ) OR ( IN 3364 'LCA p.5' );%{PE}%R 8.2.20,%MKUKATPBASIS,%CEXT_WAIT_FOR,%VEXT_WAIT_FOR,%P 2:, 4:, 5:$IN, 6:3360, 7:LCA p.1, 9:OR, 10:, 12:, 13:$IN, 14:3361, 15:LCA p.2, 17:OR, 18:, 20:, 21:$IN, 22:3362, 23:LCA p.3, 25:OR, 26:, 28:, 29:$IN, 30:3363, 31:LCA p.4, 33:OR, 34:, 36:, 37:$IN, 38:3364, 39:LCA p.5, 41:
WAIT FOR  ( $IN[3360] ) OR ( $IN[3361] ) OR ( $IN[3362] ) OR ( $IN[3363] ) OR ( $IN[3364] ) 
;ENDFOLD
;FOLD OUT 3342 'Fora area externa' State=FALSE ;%{PE}%R 8.2.20,%MKUKATPBASIS,%COUT,%VOUTX,%P 2:3342, 3:Fora area externa, 5:FALSE, 6:
$OUT[3342]=FALSE
;ENDFOLD

;FOLD PTP l031 CONT Vel=100 % P_OLP17 Tool[6]:Gripper G0100R Base[0];%{PE}%R 8.2.20,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:l031, 3:C_DIS, 5:100, 7:P_OLP17
$BWDSTART=FALSE
PDAT_ACT=PP_OLP17
FDAT_ACT=Fl031
BAS(#PTP_PARAMS,100)
PTP Xl031 C_DIS
;ENDFOLD
;FOLD PTP l036 CONT Vel=100 % P_OLP18 Tool[6]:Gripper G0100R Base[0];%{PE}%R 8.2.20,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:l036, 3:C_DIS, 5:100, 7:P_OLP18
$BWDSTART=FALSE
PDAT_ACT=PP_OLP18
FDAT_ACT=Fl036
BAS(#PTP_PARAMS,100)
PTP Xl036 C_DIS
;ENDFOLD
;FOLD PTP l041 CONT Vel=100 % P_OLP19 Tool[6]:Gripper G0100R Base[0];%{PE}%R 8.2.20,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:l041, 3:C_DIS, 5:100, 7:P_OLP19
$BWDSTART=FALSE
PDAT_ACT=PP_OLP19
FDAT_ACT=Fl041
BAS(#PTP_PARAMS,100)
PTP Xl041 C_DIS
;ENDFOLD

;FOLD PTP LCA_BUFFER Vel=100 % DEFAULT Tool[15]:AUX_LCA Base[0];%{PE}%R 8.2.20,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:LCA_BUFFER, 3:, 5:100, 7:DEFAULT
$BWDSTART=FALSE
PDAT_ACT=PDEFAULT
FDAT_ACT=FLCA_BUFFER
BAS(#PTP_PARAMS,100)
PTP XLCA_BUFFER 
;ENDFOLD

;FOLD ; RESETA CYCFLAG 1;%{PE}%R 8.2.20,%MKUKATPBASIS,%CCOMMENT,%VNORMAL,%P 2:RESETA  CYCFLAG  1
;ENDFOLD
$CYCFLAG[1]=(FALSE)

;FOLD WAIT FOR ( NOT CYCFLAG 1 '' );%{PE}%R 8.2.20,%MKUKATPBASIS,%CEXT_WAIT_FOR,%VEXT_WAIT_FOR,%P 2:, 4:NOT, 5:$CYCFLAG, 6:1, 7:, 9:
WAIT FOR  (  NOT $CYCFLAG[1] ) 
;ENDFOLD

$CYCFLAG[1]=($IN[499] AND NOT $IN[500] AND NOT $IN[489])

;FOLD ; ____________RETIRA PECA LCA POS.1_______________;%{PE}%R 8.2.20,%MKUKATPBASIS,%CCOMMENT,%VNORMAL,%P 2:____________RETIRA  PECA  LCA  POS.1_______________
;ENDFOLD
IF ($IN [3360] AND $CYCFLAG[1]) THEN
HO_LCA_ST03_POS01 ( )
ENDIF

;FOLD ; ____________RETIRA PECA LCA POS.2_______________;%{PE}%R 8.2.20,%MKUKATPBASIS,%CCOMMENT,%VNORMAL,%P 2:____________RETIRA  PECA  LCA  POS.2_______________
;ENDFOLD
IF ($IN [3361] AND $CYCFLAG[1]) THEN
HO_LCA_ST03_POS02 ( )
ENDIF

;FOLD ; ____________RETIRA PECA LCA POS.3_______________;%{PE}%R 8.2.20,%MKUKATPBASIS,%CCOMMENT,%VNORMAL,%P 2:____________RETIRA  PECA  LCA  POS.3_______________
;ENDFOLD
IF ($IN [3362] AND $CYCFLAG[1]) THEN
HO_LCA_ST03_POS03 ( )
ENDIF

;FOLD ; ____________RETIRA PECA LCA POS.4_______________;%{PE}%R 8.2.20,%MKUKATPBASIS,%CCOMMENT,%VNORMAL,%P 2:____________RETIRA  PECA  LCA  POS.4_______________
;ENDFOLD
IF ($IN [3363] AND $CYCFLAG[1]) THEN
HO_LCA_ST03_POS04 ( )
ENDIF

;FOLD ; ____________RETIRA PECA LCA POS.5_______________;%{PE}%R 8.2.20,%MKUKATPBASIS,%CCOMMENT,%VNORMAL,%P 2:____________RETIRA  PECA  LCA  POS.5_______________
;ENDFOLD
IF ($IN [3364] AND $CYCFLAG[1]) THEN
HO_LCA_ST03_POS05 ( )
ENDIF

;FOLD PTP POS_COMUM Vel=100 % PDAT10 Tool[6]:Gripper G0100R Base[0];%{PE}%R 8.2.20,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:POS_COMUM, 3:, 5:100, 7:PDAT10
$BWDSTART=FALSE
PDAT_ACT=PPDAT10
FDAT_ACT=FPOS_COMUM
BAS(#PTP_PARAMS,100)
PTP XPOS_COMUM 
;ENDFOLD

;FOLD WAIT FOR ( IN 489 'Presença de peça 1 - Axor LCA' OR Ghost() );%{PE}%R 8.2.20,%MKUKATPBASIS,%CEXT_WAIT_FOR,%VEXT_WAIT_FOR,%P 2:, 4:, 5:$IN, 6:489, 7:Presença de peça 1 - Axor LCA, 8:OR, 9:, 10:Ghost(), 11:489, 12:, 14:
WAIT FOR  ( $IN[489] OR Ghost() ) 
;ENDFOLD

;FOLD ; RESETA FLAG 3 AND CYCFLAG 1;%{PE}%R 8.2.20,%MKUKATPBASIS,%CCOMMENT,%VNORMAL,%P 2:RESETA  FLAG  3  AND  CYCFLAG  1
;ENDFOLD
$CYCFLAG[1]=(FALSE)

;FOLD WAIT FOR ( NOT CYCFLAG 1 '' );%{PE}%R 8.2.20,%MKUKATPBASIS,%CEXT_WAIT_FOR,%VEXT_WAIT_FOR,%P 2:, 4:NOT, 5:$CYCFLAG, 6:1, 7:, 9:
WAIT FOR  (  NOT $CYCFLAG[1] ) 
;ENDFOLD

;FOLD PTP l085 CONT Vel=100 % P_OLP13 Tool[6]:Gripper G0100R Base[0];%{PE}%R 8.2.20,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:l085, 3:C_DIS, 5:100, 7:P_OLP13
$BWDSTART=FALSE
PDAT_ACT=PP_OLP13
FDAT_ACT=Fl085
BAS(#PTP_PARAMS,100)
PTP Xl085 C_DIS
;ENDFOLD
;FOLD PTP l090 CONT Vel=100 % P_OLP14 Tool[6]:Gripper G0100R Base[0];%{PE}%R 8.2.20,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:l090, 3:C_DIS, 5:100, 7:P_OLP14
$BWDSTART=FALSE
PDAT_ACT=PP_OLP14
FDAT_ACT=Fl090
BAS(#PTP_PARAMS,100)
PTP Xl090 C_DIS
;ENDFOLD
;FOLD PTP l095 CONT Vel=100 % P_OLP15 Tool[6]:Gripper G0100R Base[0];%{PE}%R 8.2.20,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:l095, 3:C_DIS, 5:100, 7:P_OLP15
$BWDSTART=FALSE
PDAT_ACT=PP_OLP15
FDAT_ACT=Fl095
BAS(#PTP_PARAMS,100)
PTP Xl095 C_DIS
;ENDFOLD
;FOLD PTP P8 CONT Vel=100 % PDAT14 Tool[6]:Gripper G0100R Base[0];%{PE}%R 8.2.20,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:P8, 3:C_DIS, 5:100, 7:PDAT14
$BWDSTART=FALSE
PDAT_ACT=PPDAT14
FDAT_ACT=FP8
BAS(#PTP_PARAMS,100)
PTP XP8 C_DIS
;ENDFOLD

;FOLD PULSE 3349 'R2 pegou peça' State=TRUE Time=3 sec;%{PE}%R 8.2.20,%MKUKATPBASIS,%COUT,%VPULSE,%P 2:3349, 3:R2 pegou peça, 5:TRUE, 6:, 8:3
PULSE($OUT[3349], TRUE,3)
;ENDFOLD
;FOLD OUT 3342 'Fora area externa' State=TRUE ;%{PE}%R 8.2.20,%MKUKATPBASIS,%COUT,%VOUTX,%P 2:3342, 3:Fora area externa, 5:TRUE, 6:
$OUT[3342]=TRUE
;ENDFOLD

;FOLD PTP P9 Vel=100 % PDAT15 Tool[6]:Gripper G0100R Base[0];%{PE}%R 8.2.20,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:P9, 3:, 5:100, 7:PDAT15
$BWDSTART=FALSE
PDAT_ACT=PPDAT15
FDAT_ACT=FP9
BAS(#PTP_PARAMS,100)
PTP XP9 
;ENDFOLD

END