&ACCESS RVO1
&REL 124
&COMMENT Dep travessa LCA
&PARAM DISKPATH = KRC:\R1\Program\olp
&PARAM TEMPLATE = C:\KRC\Roboter\Template\vorgabe
&PARAM EDITMASK = *
DEF ab_com_01_st01_g01( )
;FOLD INI
  ;FOLD BASISTECH INI
    GLOBAL INTERRUPT DECL 3 WHEN $STOPMESS==TRUE DO IR_STOPM ( )
    INTERRUPT ON 3
    BAS (#INITMOV,0)
  ;ENDFOLD (BASISTECH INI)
;FOLD GRIPPERTECH INI
USER_GRP(0,DUMMY,DUMMY,GDEFAULT)
;ENDFOLD (GRIPPERTECH INI)
  ;FOLD USER INI
    ;Make your modifications here

  ;ENDFOLD (USER INI)
;ENDFOLD (INI)

; Caracterizacao dos meios de producao
; Changes: Deposita LCA na GeoBox
; Modelos: Axor/Atego - ETB/LTA/LTB/STB
; Versao Atual: 30/03/2016
; Name: P13.050-R3

chk_w6_wechs_ok ( )     ; CHECK GRIPPER AXOR/ATEGO

;FOLD PTP P13 CONT Vel=100 % PDAT11 Tool[8]:Tool Changer Base[0];%{PE}%R 8.2.20,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:P13, 3:C_DIS, 5:100, 7:PDAT11
$BWDSTART=FALSE
PDAT_ACT=PPDAT11
FDAT_ACT=FP13
BAS(#PTP_PARAMS,100)
PTP XP13 C_DIS
;ENDFOLD

; ABRE GRAMPOS e VERIFICA
;FOLD SET Z2.1 RPS AXOR State=OPN CONT at START Delay=1 ms;%{PE}%R 8.2.1,%MKUKATPGRP,%CGRP,%VGRP,%P 2:10, 4:1, 5:#YES, 6:GDAT26, 8:0, 10:1
TRIGGER WHEN DISTANCE=0 DELAY=1 DO H50(GRP_APO,10,1,GCONT) PRIO=-1
;ENDFOLD
;FOLD SET Z3.1 GRP AXOR State=OPN CONT at START Delay=1 ms;%{PE}%R 8.2.1,%MKUKATPGRP,%CGRP,%VGRP,%P 2:11, 4:1, 5:#YES, 6:GDAT27, 8:0, 10:1
TRIGGER WHEN DISTANCE=0 DELAY=1 DO H50(GRP_APO,11,1,GCONT) PRIO=-1
;ENDFOLD
;FOLD SET Z5.1/2/3/4 GRP AXOR State=OPN CONT at START Delay=1 ms;%{PE}%R 8.2.1,%MKUKATPGRP,%CGRP,%VGRP,%P 2:13, 4:1, 5:#YES, 6:GDAT28, 8:0, 10:1
TRIGGER WHEN DISTANCE=0 DELAY=1 DO H50(GRP_APO,13,1,GCONT) PRIO=-1
;ENDFOLD
;FOLD SET Z6.1/2/3/4 GRP AXOR State=OPN CONT at START Delay=1 ms;%{PE}%R 8.2.1,%MKUKATPGRP,%CGRP,%VGRP,%P 2:14, 4:1, 5:#YES, 6:GDAT29, 8:0, 10:1
TRIGGER WHEN DISTANCE=0 DELAY=1 DO H50(GRP_APO,14,1,GCONT) PRIO=-1
;ENDFOLD
;FOLD SET Z7.1/2 GRP AXOR State=OPN CONT at START Delay=1 ms;%{PE}%R 8.2.1,%MKUKATPGRP,%CGRP,%VGRP,%P 2:15, 4:1, 5:#YES, 6:GDAT30, 8:0, 10:1
TRIGGER WHEN DISTANCE=0 DELAY=1 DO H50(GRP_APO,15,1,GCONT) PRIO=-1
;ENDFOLD
chk_w6_rps2_opn()
chk_w6_grp3_opn()
chk_w6_grp5_opn()
chk_w6_grp6_opn()
chk_w6_grp7_opn()

;FOLD ; LIGA INTERLOCK 5 COM R5 com a garra rolete;%{PE}%R 8.2.20,%MKUKATPBASIS,%CCOMMENT,%VNORMAL,%P 2:LIGA  INTERLOCK  5  COM  R5  com  a  garra  rolete
;ENDFOLD
;FOLD Collision.Monitoring_5_On ;%{PE}%MKUKATPUSER
COLLSTOP( 5, 1 ) ;Interrupt-Controlled
;ENDFOLD

;FOLD OUT 3374 'Lib. Dep. Axor/Atego' State=TRUE ;%{PE}%R 8.2.20,%MKUKATPBASIS,%COUT,%VOUTX,%P 2:3374, 3:Lib. Dep. Axor/Atego, 5:TRUE, 6:
$OUT[3374]=TRUE
;ENDFOLD
;FOLD WAIT FOR ( IN 3374 'Lib. Deposito Axor/Atego' );%{PE}%R 8.2.20,%MKUKATPBASIS,%CEXT_WAIT_FOR,%VEXT_WAIT_FOR,%P 2:, 4:, 5:$IN, 6:3374, 7:Lib. Deposito Axor/Atego, 9:
WAIT FOR  ( $IN[3374] ) 
;ENDFOLD
;FOLD OUT 3374 'Lib. Dep. Axor/Atego' State=FALSE ;%{PE}%R 8.2.20,%MKUKATPBASIS,%COUT,%VOUTX,%P 2:3374, 3:Lib. Dep. Axor/Atego, 5:FALSE, 6:
$OUT[3374]=FALSE
;ENDFOLD

;FOLD PTP P8 CONT Vel=100 % PDAT6 Tool[8]:Tool Changer Base[0];%{PE}%R 8.2.20,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:P8, 3:C_DIS, 5:100, 7:PDAT6
$BWDSTART=FALSE
PDAT_ACT=PPDAT6
FDAT_ACT=FP8
BAS(#PTP_PARAMS,100)
PTP XP8 C_DIS
;ENDFOLD
;FOLD PTP P9 CONT Vel=100 % PDAT7 Tool[8]:Tool Changer Base[0];%{PE}%R 8.2.20,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:P9, 3:C_DIS, 5:100, 7:PDAT7
$BWDSTART=FALSE
PDAT_ACT=PPDAT7
FDAT_ACT=FP9
BAS(#PTP_PARAMS,100)
PTP XP9 C_DIS
;ENDFOLD
;FOLD PTP P10 CONT Vel=100 % PDAT8 Tool[8]:Tool Changer Base[0];%{PE}%R 8.2.20,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:P10, 3:C_DIS, 5:100, 7:PDAT8
$BWDSTART=FALSE
PDAT_ACT=PPDAT8
FDAT_ACT=FP10
BAS(#PTP_PARAMS,100)
PTP XP10 C_DIS
;ENDFOLD

; VERIFICA PRESENCA LCA OK
IF (NOT(GHOST())) THEN
;FOLD WAIT FOR ( IN 489 'Presença de peça 1 - Axor LCA' );%{PE}%R 8.2.20,%MKUKATPBASIS,%CEXT_WAIT_FOR,%VEXT_WAIT_FOR,%P 2:, 4:, 5:$IN, 6:489, 7:Presença de peça 1 - Axor LCA, 9:
WAIT FOR  ( $IN[489] ) 
;ENDFOLD
ENDIF

; ABRE GRAMPO Z4.1 "LCA"
;FOLD SET Z4.1 GRP AXOR State=OPN CONT at START Delay=1 ms;%{PE}%R 8.2.1,%MKUKATPGRP,%CGRP,%VGRP,%P 2:12, 4:1, 5:#YES, 6:GDAT32, 8:0, 10:1
TRIGGER WHEN DISTANCE=0 DELAY=1 DO H50(GRP_APO,12,1,GCONT) PRIO=-1
;ENDFOLD

;FOLD PTP l035 CONT Vel=100 % P_OLP8 Tool[8]:Tool Changer Base[0];%{PE}%R 8.2.20,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:l035, 3:C_DIS, 5:100, 7:P_OLP8
$BWDSTART=FALSE
PDAT_ACT=PP_OLP8
FDAT_ACT=Fl035
BAS(#PTP_PARAMS,100)
PTP Xl035 C_DIS
;ENDFOLD
;FOLD LIN P1 CONT Vel=0.5 m/s CPDAT1 Tool[8]:Tool Changer Base[0];%{PE}%R 8.2.20,%MKUKATPBASIS,%CMOVE,%VLIN,%P 1:LIN, 2:P1, 3:C_DIS C_DIS, 5:0.5, 7:CPDAT1
$BWDSTART=FALSE
LDAT_ACT=LCPDAT1
FDAT_ACT=FP1
BAS(#CP_PARAMS,0.5)
LIN XP1 C_DIS C_DIS
;ENDFOLD

; VERIFICA GRAMPO Z4.1 ABERTO
chk_w6_grp4_opn( )

;FOLD ; Posicao de deposito Gripper AXOR;%{PE}%R 8.2.20,%MKUKATPBASIS,%CCOMMENT,%VNORMAL,%P 2:Posicao de deposito Gripper AXOR
;ENDFOLD
;FOLD LIN P_DE09  Vel=0.25 m/s L_OLP1 Tool[6]:Gripper AXOR Base[0];%{PE}%R8.2.19,%MKUKATPBASIS,%CMOVE,%VLIN,%P 1:LIN, 2:P_DE09, 3: , 5:0.25, 7:L_OLP1
$BWDSTART = FALSE
LDAT_ACT= LL_OLP1
FDAT_ACT= FP_DE09
BAS(#CP_PARAMS,0.25)
LIN XP_DE09  
;ENDFOLD

IF (NOT(GHOST())) THEN
;FOLD WAIT FOR ( IN 490 'Presença de peça 2 - Axor' AND IN 491 'Presença de peça 3 - Axor' AND IN 492 'Presença de peça 4 - Axor' );%{PE}%R 8.2.20,%MKUKATPBASIS,%CEXT_WAIT_FOR,%VEXT_WAIT_FOR,%P 2:, 4:, 5:$IN, 6:490, 7:Presença de peça 2 - Axor, 8:AND, 9:, 10:$IN, 11:491, 12:Presença de peça 3 - Axor, 13:AND, 14:, 15:$IN, 16:492, 17:Presença de peça 4 - Axor, 19:
WAIT FOR  ( $IN[490] AND $IN[491] AND $IN[492] ) 
;ENDFOLD
ENDIF

;FOLD OUT 3375 'Lib. p/ sair s/ Gripper Axor/Atego' State=TRUE ;%{PE}%R 8.2.20,%MKUKATPBASIS,%COUT,%VOUTX,%P 2:3375, 3:Lib. p/ sair s/ Gripper Axor/Atego, 5:TRUE, 6:
$OUT[3375]=TRUE
;ENDFOLD

; VERIFICA PRESENCA LCA OK
IF (NOT(GHOST())) THEN
;FOLD WAIT FOR ( IN 489 'Presença de peça 1 - Axor LCA' );%{PE}%R 8.2.20,%MKUKATPBASIS,%CEXT_WAIT_FOR,%VEXT_WAIT_FOR,%P 2:, 4:, 5:$IN, 6:489, 7:Presença de peça 1 - Axor LCA, 9:
WAIT FOR  ( $IN[489] ) 
;ENDFOLD
ENDIF

; FECHA GRAMPOS
;FOLD SET Z2.1 RPS AXOR State=CLO GDAT15;%{PE}%R 8.2.1,%MKUKATPGRP,%CGRP,%VGRP,%P 2:10, 4:2, 5:#NO, 6:GDAT15, 8:0, 10:0
H50(GRP,10,2,GGDAT15)
;ENDFOLD
;FOLD SET Z3.1 GRP AXOR State=CLO GDAT8;%{PE}%R 8.2.1,%MKUKATPGRP,%CGRP,%VGRP,%P 2:11, 4:2, 5:#NO, 6:GDAT8, 8:0, 10:0
H50(GRP,11,2,GGDAT8)
;ENDFOLD
;FOLD SET Z5.1/2/3/4 GRP AXOR State=CLO GDAT16;%{PE}%R 8.2.1,%MKUKATPGRP,%CGRP,%VGRP,%P 2:13, 4:2, 5:#NO, 6:GDAT16, 8:0, 10:0
H50(GRP,13,2,GGDAT16)
;ENDFOLD
;FOLD SET Z6.1/2/3/4 GRP AXOR State=CLO GDAT17;%{PE}%R 8.2.1,%MKUKATPGRP,%CGRP,%VGRP,%P 2:14, 4:2, 5:#NO, 6:GDAT17, 8:0, 10:0
H50(GRP,14,2,GGDAT17)
;ENDFOLD

; VERIFICA GRAMPOS FECHADOS
chk_w6_rps2_clo( )
chk_w6_grp3_clo( )

; FECHA GRAMPOS Z4.1 e Z7.1
;FOLD SET Z4.1 GRP AXOR State=CLO GDAT31;%{PE}%R 8.2.1,%MKUKATPGRP,%CGRP,%VGRP,%P 2:12, 4:2, 5:#NO, 6:GDAT31, 8:0, 10:0
H50(GRP,12,2,GGDAT31)
;ENDFOLD
;FOLD SET Z7.1/2 GRP AXOR State=CLO GDAT33;%{PE}%R 8.2.1,%MKUKATPGRP,%CGRP,%VGRP,%P 2:15, 4:2, 5:#NO, 6:GDAT33, 8:0, 10:0
H50(GRP,15,2,GGDAT33)
;ENDFOLD
;FOLD SET VACUO AXOR State=BLOW GDAT34;%{PE}%R 8.2.1,%MKUKATPGRP,%CGRP,%VGRP,%P 2:9, 4:2, 5:#NO, 6:GDAT34, 8:0, 10:0
H50(GRP,9,2,GGDAT34)
;ENDFOLD
;FOLD SET VACUO AXOR State=OFF GDAT35;%{PE}%R 8.2.1,%MKUKATPGRP,%CGRP,%VGRP,%P 2:9, 4:3, 5:#NO, 6:GDAT35, 8:0, 10:0
H50(GRP,9,3,GGDAT35)
;ENDFOLD

; VERIFICA GRAMPOS FECHADOS
chk_w6_grp5_clo( )
chk_w6_grp6_clo( )
chk_w6_grp7_clo( )
chk_w6_grp4_clo( )
chk_w6_grp1_vacoff( )

; VERIFICA PRESENCA LCA OK
IF (NOT(GHOST())) THEN
;FOLD WAIT FOR ( IN 489 'Presença de peça 1 - Axor LCA' );%{PE}%R 8.2.20,%MKUKATPBASIS,%CEXT_WAIT_FOR,%VEXT_WAIT_FOR,%P 2:, 4:, 5:$IN, 6:489, 7:Presença de peça 1 - Axor LCA, 9:
WAIT FOR  ( $IN[489] ) 
;ENDFOLD
ENDIF

;FOLD OUT 3375 'Lib. p/ sair s/ Gripper Axor/Atego' State=TRUE ;%{PE}%R 8.2.20,%MKUKATPBASIS,%COUT,%VOUTX,%P 2:3375, 3:Lib. p/ sair s/ Gripper Axor/Atego, 5:TRUE, 6:
$OUT[3375]=TRUE
;ENDFOLD
;FOLD WAIT FOR ( IN 3375 'Lib. p/ sair s/ Gripper Axor/Atego' );%{PE}%R 8.2.20,%MKUKATPBASIS,%CEXT_WAIT_FOR,%VEXT_WAIT_FOR,%P 2:, 4:, 5:$IN, 6:3375, 7:Lib. p/ sair s/ Gripper Axor/Atego, 9:
WAIT FOR  ( $IN[3375] ) 
;ENDFOLD
;FOLD OUT 3375 'Lib. p/ sair s/ Gripper Axor/Atego' State=FALSE ;%{PE}%R 8.2.20,%MKUKATPBASIS,%COUT,%VOUTX,%P 2:3375, 3:Lib. p/ sair s/ Gripper Axor/Atego, 5:FALSE, 6:
$OUT[3375]=FALSE
;ENDFOLD

;FOLD WAIT Time=0.5 sec;%{PE}%R 8.2.20,%MKUKATPBASIS,%CWAIT,%VWAIT,%P 3:0.5
WAIT SEC 0.5
;ENDFOLD
;FOLD OUT 224 'Bloquear MPS' State=FALSE ;%{PE}%R 8.2.20,%MKUKATPBASIS,%COUT,%VOUTX,%P 2:224, 3:Bloquear MPS, 5:FALSE, 6:
$OUT[224]=FALSE
;ENDFOLD
;FOLD OUT 223 'Desbloquear MPS' State=TRUE ;%{PE}%R 8.2.20,%MKUKATPBASIS,%COUT,%VOUTX,%P 2:223, 3:Desbloquear MPS, 5:TRUE, 6:
$OUT[223]=TRUE
;ENDFOLD
;FOLD WAIT FOR ( IN 217 'MPS Destravado' );%{PE}%R 8.2.20,%MKUKATPBASIS,%CEXT_WAIT_FOR,%VEXT_WAIT_FOR,%P 2:, 4:, 5:$IN, 6:217, 7:MPS Destravado, 9:
WAIT FOR  ( $IN[217] ) 
;ENDFOLD
;FOLD WAIT FOR NOT ( IN 221 'MPS Travado' );%{PE}%R 8.2.20,%MKUKATPBASIS,%CEXT_WAIT_FOR,%VEXT_WAIT_FOR,%P 2:NOT, 4:, 5:$IN, 6:221, 7:MPS Travado, 9:
WAIT FOR  NOT ( $IN[221] ) 
;ENDFOLD
;FOLD IBUS OFF;%{PE}%R 8.2.20,%MKUKATPBASIS,%CIBUS,%VMAP,%P 2:#OFF, 3:3
$IBUS_OFF=$IBUS_ON
$IBUS_ON=0
;ENDFOLD
;FOLD WAIT Time=0.5 sec;%{PE}%R 8.2.20,%MKUKATPBASIS,%CWAIT,%VWAIT,%P 3:0.5
WAIT SEC 0.5
;ENDFOLD

;FOLD LIN P12 Vel=0.25 m/s CPDAT6 Tool[8]:Tool Changer Base[0];%{PE}%R 8.2.20,%MKUKATPBASIS,%CMOVE,%VLIN,%P 1:LIN, 2:P12, 3:, 5:0.25, 7:CPDAT6
$BWDSTART=FALSE
LDAT_ACT=LCPDAT6
FDAT_ACT=FP12
BAS(#CP_PARAMS,0.25)
LIN XP12 
;ENDFOLD

;FOLD WAIT FOR NOT ( IN 219 'Posicao p/ Acoplamento OK' );%{PE}%R 8.2.20,%MKUKATPBASIS,%CEXT_WAIT_FOR,%VEXT_WAIT_FOR,%P 2:NOT, 4:, 5:$IN, 6:219, 7:Posicao p/ Acoplamento OK, 9:
WAIT FOR  NOT ( $IN[219] ) 
;ENDFOLD

;FOLD LIN l040 Vel=0.25 m/s L_OLP2 Tool[8]:Tool Changer Base[0];%{PE}%R 8.2.20,%MKUKATPBASIS,%CMOVE,%VLIN,%P 1:LIN, 2:l040, 3:, 5:0.25, 7:L_OLP2
$BWDSTART=FALSE
LDAT_ACT=LL_OLP2
FDAT_ACT=Fl040
BAS(#CP_PARAMS,0.25)
LIN Xl040 
;ENDFOLD

;FOLD WAIT FOR NOT ( IN 219 'Posicao p/ Acoplamento OK' );%{PE}%R 8.2.20,%MKUKATPBASIS,%CEXT_WAIT_FOR,%VEXT_WAIT_FOR,%P 2:NOT, 4:, 5:$IN, 6:219, 7:Posicao p/ Acoplamento OK, 9:
WAIT FOR  NOT ( $IN[219] ) 
;ENDFOLD
;FOLD OUT 223 'Desbloquear MPS' State=FALSE ;%{PE}%R 8.2.20,%MKUKATPBASIS,%COUT,%VOUTX,%P 2:223, 3:Desbloquear MPS, 5:FALSE, 6:
$OUT[223]=FALSE
;ENDFOLD

;FOLD LIN P2 CONT Vel=1 m/s CPDAT2 Tool[8]:Tool Changer Base[0];%{PE}%R 8.2.20,%MKUKATPBASIS,%CMOVE,%VLIN,%P 1:LIN, 2:P2, 3:C_DIS C_DIS, 5:1, 7:CPDAT2
$BWDSTART=FALSE
LDAT_ACT=LCPDAT2
FDAT_ACT=FP2
BAS(#CP_PARAMS,1)
LIN XP2 C_DIS C_DIS
;ENDFOLD
;FOLD PTP P3 CONT Vel=100 % PDAT1 Tool[8]:Tool Changer Base[0];%{PE}%R 8.2.20,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:P3, 3:C_DIS, 5:100, 7:PDAT1
$BWDSTART=FALSE
PDAT_ACT=PPDAT1
FDAT_ACT=FP3
BAS(#PTP_PARAMS,100)
PTP XP3 C_DIS
;ENDFOLD
;FOLD PTP l045 CONT Vel=100 % P_OLP9 Tool[8]:Tool Changer Base[0];%{PE}%R 8.2.20,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:l045, 3:C_DIS, 5:100, 7:P_OLP9
$BWDSTART=FALSE
PDAT_ACT=PP_OLP9
FDAT_ACT=Fl045
BAS(#PTP_PARAMS,100)
PTP Xl045 C_DIS
;ENDFOLD

;FOLD PULSE 3343 'R3 entregou peça Geo' State=TRUE Time=3 sec;%{PE}%R 8.2.20,%MKUKATPBASIS,%COUT,%VPULSE,%P 2:3343, 3:R3 entregou peça Geo, 5:TRUE, 6:, 8:3
PULSE($OUT[3343], TRUE,3)
;ENDFOLD

;FOLD PTP P14 CONT Vel=100 % PDAT12 Tool[8]:Tool Changer Base[0];%{PE}%R 8.2.20,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:P14, 3:C_DIS, 5:100, 7:PDAT12
$BWDSTART=FALSE
PDAT_ACT=PPDAT12
FDAT_ACT=FP14
BAS(#PTP_PARAMS,100)
PTP XP14 C_DIS
;ENDFOLD

;FOLD ; DESLIGA INTERLOCK 1 COM R1 NA PEGA DA GARRA 913;%{PE}%R 8.2.20,%MKUKATPBASIS,%CCOMMENT,%VNORMAL,%P 2:DESLIGA  INTERLOCK  1  COM  R1  NA  PEGA  DA  GARRA  913
;ENDFOLD
;FOLD Collision.Monitoring_1_Off . ;%{PE}%MKUKATPUSER
COLLSTOP( 1, 2+0 )
;ENDFOLD

;FOLD ; DESLIGA INTERLOCK 5 COM R5;%{PE}%R 8.2.20,%MKUKATPBASIS,%CCOMMENT,%VNORMAL,%P 2:DESLIGA  INTERLOCK  5  COM  R5
;ENDFOLD
;FOLD Collision.Monitoring_5_Off . ;%{PE}%MKUKATPUSER
COLLSTOP( 5, 2+0 )
;ENDFOLD

;FOLD OUT 3374 'Lib. Dep. Axor/Atego' State=TRUE ;%{PE}%R 8.2.20,%MKUKATPBASIS,%COUT,%VOUTX,%P 2:3374, 3:Lib. Dep. Axor/Atego, 5:TRUE, 6:
$OUT[3374]=TRUE
;ENDFOLD
;FOLD OUT 3348 'Fora area GEOBOX' State=TRUE ;%{PE}%R 8.2.20,%MKUKATPBASIS,%COUT,%VOUTX,%P 2:3348, 3:Fora area GEOBOX, 5:TRUE, 6:
$OUT[3348]=TRUE
;ENDFOLD

END